#include <ArduinoBLE.h>

#define LIDAR_RX_PIN 10
#define LIDAR_TX_PIN 11

const int distanceThreshold = 30;
const unsigned long reportInterval = 100;
unsigned long previousMillis = 0;

BLEService relayService("180F");
BLECharacteristic relayCharacteristic("2A19", BLERead | BLEWrite, 32);

void setup() {
  Serial.begin(115200);
  Serial1.begin(115200);

  if (!BLE.begin()) {
    Serial.println("BLE failed to start!");
    while (1);
  }

  relayService.addCharacteristic(relayCharacteristic);
  BLE.addService(relayService);

  BLE.setLocalName("ParkLink Node B");
  BLE.advertise();  // Continuous advertising

  Serial.println("BLE advertising started.");
}

void loop() {
  unsigned long currentMillis = millis();

  if (currentMillis - previousMillis >= reportInterval) {
    previousMillis = currentMillis;

    int distance = readLidarDistance();
    Serial.print("Distance: ");
    Serial.println(distance);

    const char* message = (distance > 0 && distance < distanceThreshold)
                          ? "Parking Spot Unavailable"
                          : "Parking Spot Available";

    relayCharacteristic.setValue(message);
    relayCharacteristic.writeValue(message);

    Serial.println("Status sent: " + String(message));
  }

  delay(10);
}

int readLidarDistance() {
  if (Serial1.available() >= 9) {
    byte header1 = Serial1.read();
    byte header2 = Serial1.read();

    if (header1 == 0x59 && header2 == 0x59) {
      byte dist_L = Serial1.read();
      byte dist_H = Serial1.read();
      int dist = (dist_H << 8) + dist_L;

      for (int i = 0; i < 5; i++) if (Serial1.available()) Serial1.read();
      return dist;
    } else {
      while (Serial1.available()) Serial1.read();
      return -1;
    }
  }
  return -1;
}
